// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.android_tools = '4.0.1'
    ext.kotlin_version = '1.4.10'
    ext.android_junit5_version = '1.7.1.1'
    ext.junit5 = '5.3.2'
    ext.assertj_version = '3.13.2'
    ext.mockk_version = '1.10.0'
    ext.gradle_maven_publish = '0.11.1'
    ext.purchases_version = '5.0.0-amazon.alpha.6.billing3.iap2'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$android_tools"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:$android_junit5_version"
        classpath "com.vanniktech:gradle-maven-publish-plugin:$gradle_maven_publish"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "com.vanniktech.maven.publish"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName "2.0.0-amazon.alpha.6.billing3.iap2"

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Add Kotlin source directory to all source sets
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.annotation:annotation:1.2.0'

    api "com.revenuecat.purchases:purchases:$purchases_version"
    api "com.revenuecat.purchases:purchases-core-common:$purchases_version"
    api "com.revenuecat.purchases:purchases-core-utils:$purchases_version"
    api "com.revenuecat.purchases:purchases-store-amazon:$purchases_version"

    // assertion
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    testImplementation "org.assertj:assertj-core:$assertj_version"

    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.revenuecat.purchases:purchases-store-google:$purchases_version"

    // junit 5
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit5"
}

repositories {
    google()
    jcenter()
}

// Call passing parameter -PpurchasesPath="$HOME/Development/repos/purchases-android"
task enableLocalBuild {
    group = 'Tools'
    description = 'Enable composite build'
    doLast {
        new File(".composite-enable").text = purchasesPath
    }
}

task disableLocalBuild {
    group = 'Tools'
    description = 'Disable composite build'
    doLast {
        File file = file(".composite-enable")
        if (file.exists()) {
            file.delete()
        }
    }
}
